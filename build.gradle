buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url "https://files.minecraftforge.net/maven" }
		maven { url "https://plugins.gradle.org/m2/" }
		maven { url "https://dist.creeper.host/Sponge/maven" }
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
		classpath 'com.wynprice.cursemaven:CurseMaven:1.2.+'
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'
apply plugin: 'com.wynprice.cursemaven'
apply plugin: 'org.spongepowered.mixin'

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'

def ENV = System.getenv()
version = ENV.BUILD_NUMBER ? "${mod_version}.${ENV.BUILD_NUMBER}" : "${mod_version}.custom"
group = project.mod_package
archivesBaseName = project.mod_name

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	mappings channel: 'snapshot', version: project.mapping_version
	//accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	runs {
		client {
			workingDirectory project.file('run')
			args '--mixin.config', 'kubejs.mixins.json'
			//property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'

			if (project.hasProperty('mc_dev_uuid')) args '--uuid', project.mc_dev_uuid
			if (project.hasProperty('mc_dev_username')) args '--username', project.mc_dev_username
			if (project.hasProperty('mc_dev_width')) args '--width', project.mc_dev_width
			if (project.hasProperty('mc_dev_height')) args '--height', project.mc_dev_height

			mods {
				kubejs {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
			args '--mixin.config', 'kubejs.mixins.json'
			//property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'

			args 'nogui'

			mods {
				kubejs {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')
			args '--mixin.config', 'kubejs.mixins.json'
			//property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'

			args '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/')

			mods {
				kubejs {
					source sourceSets.main
				}
			}
		}
	}
}

repositories {
	mavenLocal()
	maven { url "https://dist.creeper.host/Sponge/maven" } //Mixin
}

dependencies {
	minecraft "net.minecraftforge:forge:${forge_version}"
	compile 'org.spongepowered:mixin:0.8-SNAPSHOT'
	compile fg.deobf(curse.resolve('jei', project.jei_file))
	compileOnly fg.deobf(curse.resolve('bookshelf', project.bookshelf_file))
	compileOnly fg.deobf(curse.resolve('game-stages', project.gamestages_file))
	runtimeOnly fg.deobf(curse.resolve('immersive-engineering', project.ie_file))
}

jar {
	manifest {
		attributes([
				"MixinConfigs"            : "kubejs.mixins.json",
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
	type 'jar'
	builtBy 'reobfJar'
}

if (ENV.LOCAL_MAVEN) {
	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "file://${ENV.LOCAL_MAVEN}")
			}
		}
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = "${curseforge_id}"
			releaseType = "release"
			addGameVersion "1.15.2"
			relations {
				requiredDependency 'mixinbootstrap'
				optionalDependency 'jei'
				optionalDependency 'game-stages'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

mixin {
	add sourceSets.main, "kubejs.refmap.json"
}