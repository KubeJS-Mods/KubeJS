plugins {
	id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
	id "io.github.juuxel.loom-quiltflower" version "1.7.2" apply false
	id "com.github.johnrengelman.shadow" version "7.0.0" apply false
	id "com.matthewprenger.cursegradle" version "1.4.0" apply false
}

subprojects {
	apply plugin: "dev.architectury.loom"
	apply plugin: "io.github.juuxel.loom-quiltflower"

	loom {
		silentMojangMappingsLicense()
	}

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings loom.layered() {
			officialMojangMappings()
			// parchment("org.parchmentmc.data:parchment-1.17.1:2021.10.10")
		}
	}

	if (project.path != ':common') {
		apply plugin: "com.github.johnrengelman.shadow"
		apply plugin: "com.matthewprenger.cursegradle"

		loom {
			runConfigs.configureEach { ideConfigGenerated = true }
			mods {
				main { // to match the default mod generated for Forge
					sourceSet project.sourceSets.main
					sourceSet project(':common').sourceSets.main
				}
			}
		}

		configurations {
			bundle {
				canBeResolved(true)
				canBeConsumed(false)
			}
		}

		jar {
			archiveClassifier.set "dev-slim"
		}

		shadowJar {
			archiveClassifier.set "dev-shadow"
			configurations = [
					project.configurations.bundle
			]
		}

		remapJar {
			dependsOn shadowJar
			inputFile.set shadowJar.archiveFile
			archiveBaseName.set "${rootProject.archives_base_name}-${project.name}"
			archiveClassifier.set null
		}
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "maven-publish"
	apply from: "https://files.latmod.com/public/markdown-git-changelog.gradle"

	ext.ENV = System.getenv()

	version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
	group = project.maven_group
	archivesBaseName = project.archives_base_name

	sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '17'

	compileJava {
		options.encoding = "UTF-8"
	}

	repositories {
		maven {
			url "https://maven.architectury.dev/"
		}

		maven {
			url "https://maven.saps.dev/minecraft"
			content {
				includeGroup "dev.latvian.mods"
				includeGroup "dev.ftb.mods"
			}
		}

		maven {
			url = "https://maven.parchmentmc.net/"
			content {
				includeGroup "org.parchmentmc.data"
			}
		}

		mavenLocal()
	}

	java {
		withSourcesJar()
	}
}
