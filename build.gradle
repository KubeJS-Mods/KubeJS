import java.time.Instant

plugins {
	id 'base'
	id 'java'
	id 'maven-publish'
	id 'net.neoforged.moddev' version "2.0.10-beta"
	// https://maven.architectury.dev/me/shedaniel/unified-publishing/maven-metadata.xml
	id "me.shedaniel.unified-publishing" version "0.1.13"
}

ext.ENV = System.getenv()
def isLocal = !ENV.containsKey("GITHUB_RUN_NUMBER")

version = "${mod_version}-${isLocal ? "local.${Instant.now().epochSecond}" : "build.${ENV.GITHUB_RUN_NUMBER}"}"
group = project.maven_group
base.archivesBaseName = project.archives_base_name

neoForge {
	version = project.neoforge_version
	accessTransformers {
		publish(project.file('src/main/resources/META-INF/accesstransformer.cfg'))
	}

	interfaceInjectionData {
		from(file('interfaces.json'))
		publish(file('interfaces.json'))
	}

	runs {
		configureEach {
			logLevel = org.slf4j.event.Level.INFO
		}

		client {
			client()

			if (ENV.MC_CLIENT_ARGS) {
				programArguments.addAll(ENV.MC_CLIENT_ARGS.split(' '))
			}

			jvmArguments.addAll("-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition")
			gameDirectory = file 'run'
		}

		server {
			server()
			programArgument("--nogui")
			gameDirectory = file 'run_server'
		}
	}

	mods {
		kubejs {
			sourceSet sourceSets.main
		}
	}

	parchment {
		minecraftVersion = parchment_mc_version
		mappingsVersion = parchment_mapping_version
	}
}

compileJava {
	options.encoding = "UTF-8"
	options.release.set(21)
}

java {
	sourceCompatibility = targetCompatibility = '21'
	withSourcesJar()
}

repositories {
	mavenLocal()
	mavenCentral()

	maven {
		url "https://maven.neoforged.net/releases"
	}

	maven {
		url "https://maven.architectury.dev/"
	}

	maven {
		url "https://maven.latvian.dev/releases"
		content {
			includeGroup "dev.latvian.mods"
			includeGroup "dev.latvian.apps"
		}
	}

	maven {
		url "https://maven.blamejared.com"
		content {
			includeGroup "mezz.jei"
			includeGroup "net.darkhax.bookshelf"
			includeGroup "net.darkhax.gamestages"
		}
	}

	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
		content {
			includeGroup "org.parchmentmc.data"
		}
	}

	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
		content {
			includeGroup "dev.emi"
		}
	}

	maven {
		url 'https://jitpack.io'
		content {
			includeGroup "com.github.rtyley"
		}
	}
}

dependencies {
	api("dev.latvian.mods:rhino:$rhino_version") { transitive(false) }

	compileOnly("dev.architectury:architectury-neoforge:$architectury_version")
	compileOnly("mezz.jei:jei-1.21-neoforge:$jei_version")
	compileOnly("me.shedaniel:RoughlyEnoughItems-neoforge:$rei_version")
	compileOnly("dev.emi:emi-neoforge:$emi_version:api")

	additionalRuntimeClasspath(jarJar(implementation('dev.latvian.apps:tiny-java-server'))) {
		version {
			strictly "[$tiny_server_version,)"
			prefer "$tiny_server_version"
		}
	}

	additionalRuntimeClasspath(jarJar(implementation("com.github.rtyley:animated-gif-lib-for-java"))) {
		version {
			strictly "[animated-gif-lib-$gif_lib_version,)"
			prefer "animated-gif-lib-$gif_lib_version"
		}
	}
}

processResources {
	def toReplace = [
			"version"      : project.version,
			"rhino_version": project.rhino_version,
	]

	println("[Process Resources] Replacing properties in resources: " + toReplace)

	inputs.properties toReplace
	filesMatching("META-INF/neoforge.mods.toml") {
		expand toReplace
	}
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

unifiedPublishing {
	project {
		releaseType = "${ENV.RELEASE_TYPE ?: 'release'}"
		gameVersions = [
				rootProject.minecraft_version,
				'1.21'
		]
		gameLoaders = ["neoforge"]
		displayName = "$mod_name NeoForge $project.version"
		changelog = "https://kubejs.com/changelog?mc=${project.minecraft_version}"
		// FIXME this is a guess, it was loom's remapJar
		mainPublication jar

		relations {
			depends {
				curseforge = modrinth = "rhino"
			}
		}

		if (ENV.CURSEFORGE_KEY) {
			curseforge {
				token = ENV.CURSEFORGE_KEY
				id = project.curseforge_id
			}
		}

		if (ENV.MODRINTH_TOKEN) {
			modrinth {
				token = ENV.MODRINTH_TOKEN
				id = project.modrinth_id
				version = "$project.version+$project.name"
			}
		}
	}
}

publishing {
	publications {
		mavenNeoForge(MavenPublication) {
			artifactId = archives_base_name
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_TOKEN) {
			maven {
				url "https://maven.latvian.dev/releases"
				credentials {
					username = "lat"
					password = "${ENV.MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/releases"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}

afterEvaluate {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xmaxerrs" << "1000"
	}
}