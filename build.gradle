import java.time.Instant

plugins {
	id 'base'
	id 'architectury-plugin' version '3.4-SNAPSHOT'
	id "dev.architectury.loom" version "1.3-SNAPSHOT" apply false
	id "io.github.juuxel.loom-vineflower" version "1.11.0" apply false
	id "com.github.johnrengelman.shadow" version "7.0.0" apply false
	id "me.shedaniel.unified-publishing" version "0.1.+" apply false
}

architectury {
	minecraft = project.minecraft_version
	injectInjectables = false
}

allprojects {
	ext.ENV = System.getenv()

	repositories {
		maven {
			url "https://maven.architectury.dev/"
		}

		maven {
			url "https://maven.latvian.dev/releases"
			content {
				includeGroup "dev.latvian.mods"
			}
		}

		maven {
			url "https://maven.saps.dev/releases"
			content {
				includeGroup "dev.latvian.mods"
				includeGroup "dev.ftb.mods"
			}
		}

		// jitpack (mixinextras only)
		maven {
			url "https://jitpack.io"
			content {
				includeGroup "com.github.llamalad7.mixinextras"
			}
		}

		mavenLocal()
	}
}

version = "${mod_version}-${ENV.GITHUB_RUN_NUMBER ? 'build.' + ENV.GITHUB_RUN_NUMBER : 'local.' + Instant.now().epochSecond}"
group = project.maven_group
base.archivesBaseName = project.archives_base_name

subprojects {
	apply plugin: "java"
	apply plugin: "dev.architectury.loom"
	apply plugin: 'architectury-plugin'
	apply plugin: "io.github.juuxel.loom-vineflower"
	apply plugin: "maven-publish"
	apply from: "https://files.latvian.dev/public/markdown-git-changelog.gradle"

	version = rootProject.version
	group = rootProject.group
	base.archivesBaseName = rootProject.base.archivesBaseName

	loom {
		silentMojangMappingsLicense()
	}

	compileJava {
		options.encoding = "UTF-8"
		options.release.set(17)
	}

	java {
		sourceCompatibility = targetCompatibility = '17'
		withSourcesJar()
	}

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings loom.layered() {
			officialMojangMappings()
			// parchment("org.parchmentmc.data:parchment-1.17.1:2021.10.10")
		}
	}
}

subprojects {
	if (project.path != ':common') {
		apply plugin: "com.github.johnrengelman.shadow"

		/*loom {
			runConfigs.configureEach { ideConfigGenerated = true }
			mods {
				main { // to match the default mod generated for Forge
					sourceSet project.sourceSets.main
					sourceSet project(':common').sourceSets.main
				}
			}
			mixin {
				defaultRefmapName = "kubejs.refmap.json"
			}
		}*/

		loom {
			runs {
				client {
					if (ENV.MC_CLIENT_ARGS) {
						programArgs ENV.MC_CLIENT_ARGS.split(' ')
					}

					vmArgs("-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition")

					if (ENV.MC_SHARED_ARCH_RUN && ENV.MC_SHARED_ARCH_RUN == "true") {
						runDir = '../run'
					}
				}

				server {
					if (ENV.MC_SHARED_ARCH_RUN && ENV.MC_SHARED_ARCH_RUN == "true") {
						runDir = '../run_server'
					}
				}
			}
		}

		configurations {
			bundle {
				canBeResolved(true)
				canBeConsumed(false)
			}

			common
			compileClasspath.extendsFrom common
			runtimeClasspath.extendsFrom common
		}

		jar {
			archiveClassifier.set "dev"
		}

		shadowJar {
			archiveClassifier.set "dev-shadow"
			configurations = [
					project.configurations.bundle
			]
		}

		remapJar {
			dependsOn shadowJar
			inputFile.set shadowJar.archiveFile
			archiveBaseName.set "${rootProject.archives_base_name}-${project.name}"
			archiveClassifier.set null
		}

		sourcesJar {
			from(project(':common').sourceSets.main.allSource) {
				exclude("kubejs.accesswidener") // duplicate on fabric, unneeded on forge
			}
		}
	}
}

task collectJars(type: Copy) {
	subprojects {
		if (project.path != ":common") {
			def remapJar = project.tasks.named('remapJar')
			dependsOn remapJar
			from remapJar
		}
	}

	into(buildDir.toPath().resolve("libs"))
}

assemble {
	dependsOn(collectJars)
}
