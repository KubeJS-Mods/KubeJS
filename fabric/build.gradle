plugins {
	id 'me.shedaniel.unified-publishing'
}

def generatedResources = file("src/generated/resources")
def accessWidenerFile = project(":common").file("src/main/resources/kubejs.accesswidener")

architectury {
	platformSetupLoomIde()
	fabric()
}

configurations {
	developmentFabric.extendsFrom common
	configureEach {
		resolutionStrategy {
			force "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
		}
	}
}

loom {
	accessWidenerPath = accessWidenerFile
}

dependencies {
	modApi("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")
	modApi("net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}")
	modApi("net.fabricmc.fabric-api:fabric-api-deprecated:${rootProject.fabric_api_version}")

	modApi("dev.architectury:architectury-fabric:${rootProject.architectury_version}")

	modApi("dev.latvian.mods:rhino-fabric:${rootProject.rhino_version}")

	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${rootProject.rei_version}")
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rootProject.rei_version}")

	modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:${rootProject.rei_version}")

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	bundle(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

	include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${rootProject.mixin_extras}")))
}

// The AW file is needed in :fabric project resources when the game is run.
task copyAccessWidener(type: Copy) {
	from accessWidenerFile
	into generatedResources
}

sourceSets {
	main {
		// compiledBy copyAccessWidener // I think this broke things even more
		resources {
			// srcDir generatedResources
			srcDir copyAccessWidener
		}
	}
}

processResources {
	dependsOn copyAccessWidener

	def toReplace = [
			"version"             : project.version,
			"min_rhino_version"   : rootProject.min_rhino_version,
			"architectury_version": rootProject.architectury_version
	]

	inputs.properties toReplace
	filesMatching("fabric.mod.json") {
		expand toReplace
	}
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

unifiedPublishing {
	project {
		releaseType = "${ENV.RELEASE_TYPE ?: 'release'}"
		gameVersions = [
				rootProject.minecraft_version
		]
		gameLoaders = ["fabric", "quilt"]
		displayName = "$mod_name Fabric $project.version"
		changelog = getGitChangelog()
		mainPublication remapJar

		relations {
			depends {
				curseforge = modrinth = "architectury-api"
			}
			depends {
				curseforge = modrinth = "rhino"
			}
			depends {
				curseforge = modrinth = "fabric-api"
			}
		}

		if (ENV.CURSEFORGE_KEY) {
			curseforge {
				token = ENV.CURSEFORGE_KEY
				id = project.curseforge_id
			}
		}

		if (ENV.MODRINTH_TOKEN) {
			modrinth {
				token = ENV.MODRINTH_TOKEN
				id = project.modrinth_id
				version = "$project.version+$project.name"
			}
		}
	}
}

publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_TOKEN) {
			maven {
				url "https://maven.latvian.dev/releases"
				credentials {
					username = "lat"
					password = "${ENV.MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/releases"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}
