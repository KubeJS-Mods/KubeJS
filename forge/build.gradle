plugins {
	id 'me.shedaniel.unified-publishing'
}

architectury {
	platformSetupLoomIde()
	forge()
}

configurations {
	developmentForge.extendsFrom common
}

loom {
	accessWidenerPath = project(":common").file("src/main/resources/kubejs.accesswidener")
	forge {
		convertAccessWideners = true
		extraAccessWideners.add("kubejs.accesswidener")
		mixinConfigs = [
				"kubejs-forge.mixins.json",
				"kubejs-common.mixins.json"
		]
	}

	interfaceInjection {
		interfaceInjectionSourceSets.add project(':common').sourceSets.main
	}
}

repositories {
	maven {
		name "ModMaven (JEI)"
		url "https://modmaven.dev/"
		content {
			includeGroup "net.darkhax.bookshelf"
			includeGroup "net.darkhax.gamestages"
		}
	}
	maven {
		url "https://maven.blamejared.com"
		content {
			includeGroup "mezz.jei"
		}
	}
}

dependencies {
	forge("net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}")

	modApi("dev.architectury:architectury-forge:${rootProject.architectury_version}")

	modApi("dev.latvian.mods:rhino-forge:${rootProject.rhino_version}") { transitive false }

	modLocalRuntime modCompileOnly("net.darkhax.bookshelf:Bookshelf-Forge-1.19.2:16.1.4")
	modLocalRuntime modCompileOnly("net.darkhax.gamestages:GameStages-Forge-1.19.2:11.0.2")

	modCompileOnly("mezz.jei:jei-1.19.2-forge:${rootProject.jei_version}") {
		transitive(false)
	}

	modLocalRuntime modCompileOnly("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}")
	// modLocalRuntime modCompileOnly("me.shedaniel:RoughlyEnoughItems-plugin-compatibilities-forge:${rootProject.rei_comp_version}")

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	bundle(project(path: ":common", configuration: "transformProductionForge")) { transitive false }

	implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${rootProject.mixin_extras}"))
	implementation(include("io.github.llamalad7:mixinextras-forge:${rootProject.mixin_extras}"))
}

processResources {
	def toReplace = [
			"version"             : project.version,
			"rhino_version"       : rootProject.rhino_version,
			"architectury_version": rootProject.architectury_version
	]

	inputs.properties toReplace
	filesMatching("META-INF/mods.toml") {
		expand toReplace
	}
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

unifiedPublishing {
	project {
		releaseType = "${ENV.RELEASE_TYPE ?: 'release'}"
		gameVersions = [
				rootProject.minecraft_version
		]
		gameLoaders = ["forge"]
		displayName = "$mod_name Forge $project.version"
		changelog = getGitChangelog()
		mainPublication remapJar

		relations {
			depends {
				curseforge = modrinth = "architectury-api"
			}
			depends {
				curseforge = modrinth = "rhino"
			}
		}

		if (ENV.CURSEFORGE_KEY) {
			curseforge {
				token = ENV.CURSEFORGE_KEY
				id = project.curseforge_id
			}
		}

		if (ENV.MODRINTH_TOKEN) {
			modrinth {
				token = ENV.MODRINTH_TOKEN
				id = project.modrinth_id
				version = "$project.version+$project.name"
			}
		}
	}
}

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_TOKEN) {
			maven {
				url "https://maven.latvian.dev/releases"
				credentials {
					username = "lat"
					password = "${ENV.MAVEN_TOKEN}"
				}
			}
		}

		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/releases"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}
