plugins {
	id 'me.shedaniel.unified-publishing'
}

architectury {
	platformSetupLoomIde()
	forge()
}

configurations {
	developmentForge.extendsFrom common
}

loom {
	accessWidenerPath = project(":common").file("src/main/resources/kubejs.accesswidener")
	forge {
		convertAccessWideners = true
		extraAccessWideners.add("kubejs.accesswidener")
		mixinConfigs = [
				"kubejs-forge.mixins.json",
				"kubejs-common.mixins.json"
		]
	}
}

repositories {
	maven {
		name "ModMaven"
		url "https://modmaven.dev/"
		content {
			includeGroup "net.darkhax.bookshelf"
			includeGroup "net.darkhax.gamestages"
		}
	}
	maven {
		name "JEI"
		url "https://dvs1.progwml6.com/files/maven/"
		content {
			includeGroup "mezz.jei"
		}
	}
}

dependencies {
	forge("net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}")

	modApi("dev.architectury:architectury-forge:${rootProject.architectury_version}")

	modApi("dev.latvian.mods:rhino-forge:${rootProject.rhino_version}") { transitive false }

	modLocalRuntime modCompileOnly("net.darkhax.bookshelf:Bookshelf-Forge-1.18.2:13.2.48")
	modLocalRuntime modCompileOnly("net.darkhax.gamestages:GameStages-Forge-1.18.2:8.0.2")

	modCompileOnly("mezz.jei:jei-1.18.2-common-api:${rootProject.forge_jei_version}")
	modCompileOnly("mezz.jei:jei-1.18.2-forge-api:${rootProject.forge_jei_version}")
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}")

	def jeiAtRuntime = true
	if (jeiAtRuntime) {
		modLocalRuntime("mezz.jei:jei-1.18.2-forge:${rootProject.forge_jei_version}") {transitive false}
	} else {
		modLocalRuntime("me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}")
	}

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	bundle(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
}

processResources {
	def toReplace = [
			"version"             : project.version,
			"rhino_version"       : rootProject.rhino_version,
			"architectury_version": rootProject.architectury_version
	]

	inputs.properties toReplace
	filesMatching("META-INF/mods.toml") {
		expand toReplace
	}
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

unifiedPublishing {
	project {
		releaseType = rootProject.artifact_type
		gameVersions = [
				rootProject.minecraft_version
		]
		gameLoaders = ["forge"]
		displayName = "$mod_name Forge $project.version"
		changelog = getGitChangelog()
		mainPublication remapJar

		relations {
			depends {
				curseforge = modrinth = "architectury-api"
			}
			depends {
				curseforge = modrinth = "rhino"
			}
		}

		if (ENV.CURSEFORGE_KEY) {
			curseforge {
				token = ENV.CURSEFORGE_KEY
				id = project.curseforge_id
			}
		}

		if (ENV.MODRINTH_TOKEN) {
			modrinth {
				token = ENV.MODRINTH_TOKEN
				id = project.modrinth_id
				version = "$project.version+$project.name"
			}
		}
	}
}

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			from components.java
		}
	}

	repositories {
		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "latvian"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}
